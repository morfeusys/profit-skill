root:
    $WhatsBetter? $left_PA (или|либо)? ($right|$right_PA)?
    $WhatsBetter? $left_AP (или|либо)? ($right|$right_AP)?

filler:
    отдать | купить | заплатить | приобрести | взять

slots:
    first_amount:
        source: $Amount1
        type: YANDEX.NUMBER
    first_unit:
        source: $Unit1
        type: UnitName
    first_price:
        source: $Price1
        type: YANDEX.NUMBER
    second_amount:
        source: $Amount2
        type: YANDEX.NUMBER
    second_unit:
        source: $Unit2
        type: UnitName
    second_price:
        source: $Price2
        type: YANDEX.NUMBER

$WhatsBetter:
    что? выгоднее (приобрести | взять)?

$left_PA:
    $Price1 $Currency? за $Amount1? $Unit1
$left_AP:
    $Amount1? $Unit1 за $Price1 $Currency?

$right:
    $Price2 $Currency? за $Amount2? $Unit2
    $Amount2? $Unit2 за $Price2 $Currency?

$right_PA:
    $Price2 за $Amount2

$right_AP:
    $Amount2 за $Price2

$Amount1:
    $YANDEX.NUMBER
$Amount2:
    $YANDEX.NUMBER
$Price1:
    $YANDEX.NUMBER
$Price2:
    $YANDEX.NUMBER
$Unit1:
    %lemma
    $UnitName
$Unit2:
    %lemma
    $UnitName
$Currency:
    %lemma
    рубль
    %exact
    руб
    р
